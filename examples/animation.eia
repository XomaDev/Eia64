; TrashGuy Animation written in Eia64!
; You type in some text in the terminal and he steals them! ðŸ‘€
; https://zac.cy/trashguy/

include(
  static:std:string
)

private let TRASH = "ðŸ—‘"
private let HAPPY_RIGHT = "(> ^_^)>"
private let HAPPY_LEFT = "<(^_^ <)"
private let INTERVAL = 200

private fn trash(text: String) {
    let size = len(text)

    for (var i = 0, i < size, i++) {
        var l = text[0]
        var going_right = true

        var left_space = 0
        var right_space = 10 + i

        until (true) {
            println(TRASH,
                " ".repeat(left_space),
                if (going_right) HAPPY_RIGHT else l + HAPPY_LEFT,
                " ".repeat(right_space),
                text)
            sleep(INTERVAL)

            if (going_right && left_space >= 10 + i) {
                going_right = false
                left_space = 10 + i
                right_space = 0

                text = text.substring(1)
            } else if (!going_right && right_space >= 10 + i) {
                println(TRASH, HAPPY_LEFT, " ".repeat(10 + i + 1), text)
                sleep(INTERVAL)
                println("breaking! next itr should be " + (i + 1))
                break
            }
            if (going_right) {
                left_space++
                right_space--
            } else {
                left_space--
                right_space++
            }
        }
    }
}
until (true) {
    print("Enter a word: ")
    trash(readln())
}